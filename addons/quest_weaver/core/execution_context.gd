# res://addons/quest_weaver/core/execution_context.gd
class_name ExecutionContext
extends RefCounted

## A data container that bundles all external dependencies for the execution
## of a quest graph. It is created once and then passed through the execution flow.

var game_state: Node
var services: Node

var logger: QWLogger:
	get:
		if is_instance_valid(services) and services.get("logger"):
			return services.logger
		return null

var _controller_weak: WeakRef 
var quest_controller: QuestController:
	get:
		if _controller_weak:
			return _controller_weak.get_ref() as QuestController
		return null

var item_objective_listeners: Dictionary = {}
var kill_objective_listeners: Dictionary = {}
var interact_objective_listeners: Dictionary = {}
var location_objective_listeners: Dictionary = {} 

func _init(p_controller: QuestController, p_game_state: Node, p_logger: QWLogger, p_services: Node) -> void:
	self._controller_weak = weakref(p_controller)
	self.game_state = p_game_state
	self.services = p_services

func cleanup() -> void:
	game_state = null
	services = null
	_controller_weak = null
	
	item_objective_listeners.clear()
	kill_objective_listeners.clear()
	interact_objective_listeners.clear()
	location_objective_listeners.clear()
