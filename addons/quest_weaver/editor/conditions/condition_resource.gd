# res://addons/quest_weaver/editor/conditions/condition_resource.gd
@tool
class_name ConditionResource
extends Resource

enum QuestState { INACTIVE, ACTIVE, COMPLETED, FAILED }
enum ConditionType { 
	BOOL, CHANCE, CHECK_ITEM, CHECK_QUEST_STATUS, CHECK_VARIABLE, CHECK_SYNCHRONIZER,
	CHECK_OBJECTIVE_STATUS, COMPOUND
}
@export var type: ConditionType = ConditionType.BOOL

# --- BoolCondition ---
@export var is_true: bool = true

# --- ChanceCondition ---
@export_range(0.0, 100.0, 0.1, "suffix:%") var chance_percentage: float = 50.0

# --- CheckItemCondition ---
@export var item_id: String = ""
@export var amount: int = 1

# --- CheckQuestStatusCondition ---
@export var quest_id: String = ""
@export var expected_status: QWEnums.QuestState = QWEnums.QuestState.COMPLETED

# --- CheckVariableCondition ---
@export var variable_name: String = ""
@export var expected_value_string: String = ""
enum Operator { EQUALS, NOT_EQUALS, GREATER_THAN, LESS_THAN, GREATER_OR_EQUAL, LESS_OR_EQUAL }
@export var operator: Operator = Operator.EQUALS

# --- CheckSynchronizerCondition ---
enum CheckType { RECEIVED_N_INPUTS, RECEIVED_SPECIFIC_INPUT }
@export var check_type: CheckType = CheckType.RECEIVED_N_INPUTS
@export var sync_value: int = 1

# --- CheckObjectiveStatusCondition ---
@export var objective_id: String = ""
@export var expected_objective_status: ObjectiveResource.Status = ObjectiveResource.Status.COMPLETED

# --- CompoundCondition ---
enum LogicOperator { AND, OR }
@export var logic_operator: LogicOperator = LogicOperator.AND
@export var sub_conditions: Array[ConditionResource] = []


func check(context: Variant, instance: QuestInstance = null) -> bool:
	var controller = _get_controller_safely(context)
	
	match type:
		ConditionType.BOOL:
			return is_true
			
		ConditionType.CHANCE:
			return randf() < (chance_percentage / 100.0)
			
		ConditionType.CHECK_ITEM:
			if not is_instance_valid(controller): return false
			var adapter = controller._inventory_adapter
			if not is_instance_valid(adapter) or item_id.is_empty():
				return false
			# Resolve potential variables in item_id or amount using instance?
			# For v1.0 basic, we stick to static IDs, but here is where placeholders would be resolved.
			return adapter.check_item(item_id, amount)
			
		ConditionType.CHECK_QUEST_STATUS:
			var current_status = controller.get_quest_state(quest_id)
			#print("[DEBUG] Condition CHECK_QUEST_STATUS: ID='%s' Expected=%d Actual=%d" % [quest_id, expected_status, current_status])
			return current_status == expected_status
			
		ConditionType.CHECK_VARIABLE:
			# Priority: 1. QuestInstance Variable (Local), 2. GameState (Global)
			var actual_value = null
			
			if instance and instance.variables.has(variable_name):
				actual_value = instance.get_variable(variable_name)
			else:
				# Fallback to Global State
				if context is RefCounted and context.get("game_state"):
					if is_instance_valid(context.game_state):
						actual_value = context.game_state.get_variable(variable_name)
			
			return _compare_values(actual_value, expected_value_string)
			
		ConditionType.CHECK_OBJECTIVE_STATUS:
			if not is_instance_valid(controller): return false
			if objective_id.is_empty(): return false
			return controller.get_objective_status(objective_id) == expected_objective_status
			
		ConditionType.CHECK_SYNCHRONIZER:
			# Context here is the specialized Dictionary passed by SyncManager
			if context is Dictionary and context.has("sync_inputs_received_array"):
				var received_array: Array = context["sync_inputs_received_array"]
				match check_type:
					CheckType.RECEIVED_N_INPUTS:
						return received_array.count(true) >= sync_value
					CheckType.RECEIVED_SPECIFIC_INPUT:
						return sync_value >= 0 and sync_value < received_array.size() and received_array[sync_value]
			return false
			
		ConditionType.COMPOUND:
			if sub_conditions.is_empty(): return logic_operator == LogicOperator.AND
			match logic_operator:
				LogicOperator.AND:
					for condition in sub_conditions:
						if not is_instance_valid(condition) or not condition.check(context, instance): return false
					return true
				LogicOperator.OR:
					for condition in sub_conditions:
						if is_instance_valid(condition) and condition.check(context, instance): return true
					return false
	
	return false

func _get_controller_safely(context: Variant) -> Node:
	if context is RefCounted and context.get("quest_controller"): # ExecutionContext
		return context.quest_controller
		
	var main_loop = Engine.get_main_loop()
	if main_loop and main_loop.root:
		return main_loop.root.get_node_or_null("QuestWeaverServices") # Returns services node, need controller from it
	
	return null

func _compare_values(actual_value: Variant, expected_value_str: String) -> bool:
	if actual_value == null: return false
	
	var expected_value: Variant = _parse_string_to_variant(expected_value_str)
	
	if typeof(actual_value) != typeof(expected_value):
		if not (actual_value is float and expected_value is int) and \
		   not (actual_value is int and expected_value is float):
			return false
			
	match operator:
		Operator.EQUALS: return actual_value == expected_value
		Operator.NOT_EQUALS: return actual_value != expected_value
		Operator.GREATER_THAN: return actual_value > expected_value
		Operator.LESS_THAN: return actual_value < expected_value
		Operator.GREATER_OR_EQUAL: return actual_value >= expected_value
		Operator.LESS_OR_EQUAL: return actual_value <= expected_value
		
	return false

func _parse_string_to_variant(text: String) -> Variant:
	var parsed_value: Variant = text
	if text.is_valid_int(): parsed_value = text.to_int()
	elif text.is_valid_float(): parsed_value = text.to_float()
	elif text.to_lower() == "true": parsed_value = true
	elif text.to_lower() == "false": parsed_value = false
	return parsed_value

func to_dictionary() -> Dictionary:
	var sub_conditions_data = []
	for sub_cond in sub_conditions:
		if is_instance_valid(sub_cond):
			sub_conditions_data.append(sub_cond.to_dictionary())
	
	return {
		"@script_path": get_script().resource_path,
		"type": self.type, "is_true": self.is_true,
		"chance_percentage": self.chance_percentage, "item_id": self.item_id,
		"amount": self.amount, "quest_id": self.quest_id,
		"expected_status": self.expected_status, "variable_name": self.variable_name,
		"expected_value_string": self.expected_value_string, "operator": self.operator,
		"check_type": self.check_type, "sync_value": self.sync_value,
		"logic_operator": self.logic_operator, "objective_id": self.objective_id,
		"expected_objective_status": self.expected_objective_status, 
		"sub_conditions": sub_conditions_data,
	}

func from_dictionary(data: Dictionary):
	self.type = _defensive_load(data, "type", ConditionType.keys(), ConditionType.BOOL)
	self.is_true = data.get("is_true", true)
	self.chance_percentage = data.get("chance_percentage", 50.0)
	self.item_id = data.get("item_id", "")
	self.amount = data.get("amount", 1)
	self.quest_id = data.get("quest_id", "")
	self.expected_status = _defensive_load(data, "expected_status", QWEnums.QuestState.keys(), QWEnums.QuestState.COMPLETED)
	self.variable_name = data.get("variable_name", "")
	self.expected_value_string = data.get("expected_value_string", "")
	self.operator = _defensive_load(data, "operator", Operator.keys(), Operator.EQUALS)
	self.check_type = _defensive_load(data, "check_type", CheckType.keys(), CheckType.RECEIVED_N_INPUTS)
	self.sync_value = data.get("sync_value", 1)
	self.objective_id = data.get("objective_id", "")
	self.expected_objective_status = _defensive_load(data, "expected_objective_status", ObjectiveResource.Status.keys(), ObjectiveResource.Status.COMPLETED)
	self.logic_operator = _defensive_load(data, "logic_operator", LogicOperator.keys(), LogicOperator.AND)
	
	self.sub_conditions.clear()
	var sub_conditions_data = data.get("sub_conditions", [])
	for sub_cond_dict in sub_conditions_data:
		var script_path = sub_cond_dict.get("@script_path")
		if script_path and ResourceLoader.exists(script_path):
			var new_sub_cond = load(script_path).new()
			new_sub_cond.from_dictionary(sub_cond_dict)
			self.sub_conditions.append(new_sub_cond)

func _defensive_load(data: Dictionary, prop: String, keys: Array, default_val: int) -> int:
	var val = data.get(prop, default_val)
	if val is int and val >= 0 and val < keys.size():
		return val
	return default_val
